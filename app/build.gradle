plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.enxy.weather"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 148
        versionName "1.0.0-alpha-5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "weather-${variant.buildType.name}-${defaultConfig.versionName}(${defaultConfig.versionCode}).apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def apiKeyOpenWeatherMap = properties.getProperty("api_key_open_weather_map", "")
        def apiKeyOpenCage = properties.getProperty("api_key_open_cage", "")
        it.buildConfigField 'String', "API_KEY_OPEN_CAGE", apiKeyOpenCage
        it.resValue 'string', "api_key_open_cage", apiKeyOpenCage

        it.buildConfigField 'String', "API_KEY_OPEN_WEATHER_MAP", apiKeyOpenWeatherMap
        it.resValue 'string', "api_key_open_weather_map", apiKeyOpenWeatherMap
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }
}

dependencies {
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0-rc01'
    def retrofit2_version = "2.9.0"
    def retrofit2_converter_json_version = "2.6.1"
    def kotlin_coroutines_version = "1.3.7"
    def gson_version = "2.8.6"
    def room_version = "2.2.5"
    def material_dialogs_version = "3.3.0"
    def koin_version = "2.1.5"
    def ktx_version = "1.3.0"
    def fragment_version = "1.2.5"
    def preferences_version = "1.1.1"
    def lifecycle_version = "2.2.0"
    def junit_version = "4.13"
    def junit_androidx_version = "1.1.1"
    def arch_test_version = "2.1.0"
    def test_runner_version = "1.2.0"
    def espresso_version = "3.2.0"
    def material_version = "1.3.0-alpha01"
    def legacy_support_version = "1.0.0"
    def recyclerview_version = "1.1.0"
    def appcompat_version = "1.1.0"
    def swiperefreshlayout_version = "1.0.0"
    def constraintlayout_version = "1.1.3"


    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // UI
    implementation "com.afollestad.material-dialogs:input:$material_dialogs_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.legacy:legacy-support-core-utils:$legacy_support_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefreshlayout_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    // Tests
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.test.ext:junit:$junit_androidx_version"
    androidTestImplementation "androidx.arch.core:core-testing:$arch_test_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_androidx_version"
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test:rules:$test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    // Architecture Components
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.preference:preference-ktx:$preferences_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // Network
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_converter_json_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Room - Database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Koin - Dependency Injection
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"
}
